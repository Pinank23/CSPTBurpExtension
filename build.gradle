plugins {
    id('java')
    id("maven-publish")
}

sourceCompatibility = 17
targetCompatibility = 17

group = 'com.doyensec'
version = 'v1'

repositories {
    mavenCentral()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
}

configurations {
    antTask
}

dependencies {
    implementation 'net.portswigger.burp.extensions:montoya-api:2023.12.1'
    implementation 'com.jetbrains.intellij.java:java-gui-forms-rt:241.18034.62'
    antTask 'com.jetbrains.intellij.java:java-compiler-ant-tasks:241.18034.62'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDirectory.set(file("$buildDir/"))
}

// Make sure to rebuild .form files. From https://github.com/edward3h/systray-mpd/blob/master/build.gradle
task compileJava(type: JavaCompile, overwrite: true, dependsOn: configurations.runtimeClasspath.getTaskDependencyFromProjectDependency(true, 'jar')) {
    doLast {
        project.sourceSets.main.output.classesDirs.each { project.mkdir(it) }
        ant.taskdef name: 'javac2', classname: 'com.intellij.ant.Javac2', classpath: configurations.antTask.asPath
        ant.javac2 srcdir: project.sourceSets.main.java.srcDirs.join(':'),
                classpath: project.sourceSets.main.compileClasspath.asPath,
                destdir: project.sourceSets.main.output.classesDirs[0],
                source: sourceCompatibility,
                target: targetCompatibility,
                includeAntRuntime: false
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifact './build/CSPTBurpExtension.jar'
        }
    }
}